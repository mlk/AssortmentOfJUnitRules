buildscript {
    dependencies {
        classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.1.11'
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.8.0"
    }
    repositories {
        mavenCentral()
    }
}

apply plugin: 'java'
apply plugin: "info.solidsoft.pitest"
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'io.codearte.nexus-staging'

group = 'com.github.mlk'
version = "1.5.${System.env.TRAVIS_BUILD_NUMBER}"
sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url = "https://s3-us-west-2.amazonaws.com/dynamodb-local/release"
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

dependencies {
    compileOnly 'org.apache.spark:spark-core_2.11:2.1.1'
    compileOnly 'org.apache.hadoop:hadoop-client:2.8.0'
    compileOnly 'org.apache.hadoop:hadoop-minicluster:2.8.0'
    compileOnly 'de.flapdoodle.embed:de.flapdoodle.embed.mongo:2.0.0'
    compileOnly 'org.mongodb:mongodb-driver:3.0.2'
    compileOnly 'org.apache.sshd:sshd-core:1.4.0'
    compileOnly 'io.findify:s3mock_2.11:0.2.0'
    compileOnly 'com.amazonaws:aws-java-sdk-s3:1.11.123'
    compileOnly 'com.hierynomus:sshj:0.21.1'
    compileOnly 'com.amazonaws:DynamoDBLocal:1.11.0.1'
    compileOnly 'ru.yandex.qatools.embed:postgresql-embedded:2.2'
    compileOnly 'pl.allegro.tech:embedded-elasticsearch:2.7.0'

    compileOnly "org.projectlombok:lombok:1.16.16"

    testCompile 'org.apache.spark:spark-core_2.11:2.1.1'
    testCompile 'org.apache.hadoop:hadoop-client:2.8.0'
    testCompile 'org.apache.hadoop:hadoop-minicluster:2.8.0'
    testCompile 'de.flapdoodle.embed:de.flapdoodle.embed.mongo:2.0.0'
    testCompile 'org.mongodb:mongodb-driver:3.0.2'
    testCompile 'org.apache.sshd:sshd-core:1.4.0'
    testCompile 'io.findify:s3mock_2.11:0.2.0'
    testCompile 'com.amazonaws:aws-java-sdk-s3:1.11.123'
    testCompile 'com.hierynomus:sshj:0.21.1'
    testCompile 'com.amazonaws:DynamoDBLocal:1.11.0.1'
    testCompile 'ru.yandex.qatools.embed:postgresql-embedded:2.2'
    testCompile 'org.postgresql:postgresql:9.4.1212'
    testCompile 'pl.allegro.tech:embedded-elasticsearch:2.7.0'

    testCompile 'org.elasticsearch:elasticsearch:6.0.0'
    testCompile "org.elasticsearch.client:elasticsearch-rest-high-level-client:6.1.2"
    
    compile 'junit:junit:4.12'
}

jar {
    baseName = 'assortmentofjunitrules'
}


uploadArchives {
    if(project.hasProperty("ossrhUsername")) {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'AssortmentOfJUnitRules'
                    packaging 'jar'
                    description 'Useful assortment of JUnit rules for integration testing'
                    url 'http://github.com/mlk/AssortmentOfJUnitRules'

                    scm {
                        connection 'scm:git:http://github.com/mlk/AssortmentOfJUnitRules/'
                        url 'http://github.com/mlk/AssortmentOfJUnitRules/'
                    }

                    licenses {
                        license {
                            name 'Modified BSD License'
                            url 'http://github.com/mlk/AssortmentOfJUnitRules/license.txt'
                        }
                    }

                    developers {
                        developer {
                            id 'mlk'
                            name 'Michael Lee'
                            email 'pom@michael-lloyd-lee.me.uk'
                        }
                    }
                }
            }
        }
    }
}
